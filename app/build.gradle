apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("yyyy-MM-dd_HH-mm", TimeZone.getDefault())
}

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        applicationId "me.ewriter.bangumitv"
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion  rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
    }

    signingConfigs {

        def Properties localProps = new Properties();
        localProps.load(new FileInputStream(file("../local.properties")))
        def Properties keyProps = new Properties()

        // 读取不到local.properties 中的 信息后就使用debug 信息

        if (localProps['store']) {
            keyProps["store"] = localProps['store']
            keyProps['storePass'] = localProps['storePass']
            keyProps['alias'] = localProps['alias']
            keyProps['aliasPass'] = localProps['aliasPass']
        }

        release {
            assert localProps['store']
            storeFile file(keyProps['store'])
            storePassword keyProps['storePass']
            keyAlias keyProps['alias']
            keyPassword keyProps['aliasPass']
        }
    }

    productFlavors {
        QQ {
            manifestPlaceholders = [BUGLY_CHANNEL_VALUE : "QQ"]
        }
        GooglePlay {
            manifestPlaceholders = [BUGLY_CHANNEL_VALUE : "GooglePlay"]
        }

    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.each {output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('release.apk')) {
                // 输出apk名称为BangumiTV_v1.0_2015-01-15_wandoujia.apk
                def fileName = "BangumiTV_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile rootProject.ext.dependencies["appcompat-v7"]
    compile rootProject.ext.dependencies["design"]
    compile rootProject.ext.dependencies["recyclerview"]

    compile rootProject.ext.dependencies["picasso"]
    compile rootProject.ext.dependencies["retrofit"]
    compile rootProject.ext.dependencies["convert-gson"]
    compile rootProject.ext.dependencies["logging-interceptor"]

    compile rootProject.ext.dependencies["greendao"]
    compile rootProject.ext.dependencies["eventbus"]

    compile rootProject.ext.dependencies["circleimageview"]
    compile rootProject.ext.dependencies["customtabs"]

    compile rootProject.ext.dependencies["bugly"]

    compile rootProject.ext.dependencies["easypermissions"]
    compile rootProject.ext.dependencies["discrete-seekbar"]
}
